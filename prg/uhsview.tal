\\ UHS viewer (public domain)
\\ Requires some nonstandard features of uxn38.

\\ (This program is currently incomplete.)

|0000
  @Jstate $1
  @Mstate $1
  @MouseX $2
  @MouseY $2
  @Type $1
  \\ Pointers into line index
  @Active $2
  @Scroll $2
  @Cursor $2
  @Break $2
  @ActiveEnd $2
|0022
  @Width $1
|0024
  @Height $1
  @Tend $1
|0028
  @Title

|0100
  started
@makeindex
  \\ Load the list of lines
  LIT2r :line-1 LIT2r :lines
  &go
    \\ Update parent address if necessary
    OVR2r STH2r #06 ADD LDA2 STH2rk GTH2 [?
      SWP2r LDA2r
      STH2rk #05 ADD LDA .typ/subject EOR ;&in STA
      SWP2r !&go
    ]@
    \\ File position
    #a3 STH2rk STA STH2rk #02 DEO2
    \\ Parent address
    STA2rk
    \\ Number of lines, or - or =
    #0000 #ac DEI
    ( number* char // parent* addr* )
    isword "- -typ/break 0d
    isword "= -typ/inner 0d
    DUP #1a EQU ?&eof
  &ch
    DUP #0a EQU ?&el
    DUP #20 EQU ?&sp
    #30 SUB DUP #09 GTH ?&nn
    STH #000a MUL2 #00 STHr ADD2
    #ac DEI !&ch
  &sp
    POP #ac DEI
    DUP LIT "c NEQ [?
      POP #ac DEI
      isword "r -typ/credit "edit 0d
      isword "o -typ/comment "mment 0d
    ]@
    isword "s -typ/subject "ubject 0d
    isword "h -typ/hint "int 0d
    isword "n -typ/nesthint "esthint 0d
    isword "l -typ/link "ink 0d
    isword "b -typ/blank "lank 0d
    \\ None match, so fall through
  &nn
    POP #ac DEI DUP #0a NEQ ?&nn \\ fall through
  &el
    DUP EOR \\ fall through
  &set
    \\ Update parent address if applicable
    LIT &in 00 [? NIP2r DUP2r DUP .typ/subject EOR ,&in STR ]@
    DUP .typ/inner NEQ [? ,&in LDR DUP .subxnest NEQ MUL ,&in STR ]@
    \\ Send record
    STH2rk #05 ADD STA
    ORAk [? INC ]@
    #30 SFT2 STH2rk ADD2 STH2rk #06 ADD STA2
    LIT2r 0008 ADD2r !&go
  &eof
    POP POP2 LIT2r :line-1/final STA2r POP2r \\ continue
\\ Load and encrypt title
  ;line2 seek
  ;line3z LDA2 ;line2z LDA2 SUB2 #0002 SUB2
  DUP .Title ADD .Tend STZ #aa DEO2
  LIT2r :Title STHrk LITr ac DEO2r
  [@
    LDZk STH LIT2r :Tkey STHk LITr 03 DIVrk MULr SUBr ADDr LDAr STHk EORr ADDr
    STHrk #80 LTH [? DUPr LITr df GTHr INCr LITr 60 MULr SUBr ]@ STHk STZr
    INC DUP
  ]?
  POP ;lines
\\ This must end not before 0x0200
\_1@8>/[1E]
\\ This fall through to gonode

@gonode ( address* -- ) ( no return )
  DUP2k .Active STZ2 #0010 ADD2 DUP2 .Scroll STZ2 .Cursor STZ2
  #05 ADD LDAk .Type STZ INC2 LDA2 .ActiveEnd STZ2
@shownode
  cls showtitle
  rvoff
  LITr -Scroll LDZ2r #01
  [@
    #00 OVR locate
    ( draw cursor ) #20 STH2rk .Cursor LDZ2 EQU2 OVR SFT SUB drawch
    STH2rk .ActiveEnd LDZ2 NEQ2 [?
      printi "--End-- 00
      !&end
    ]@
    STH2rk .Break LDZ2 NEQ2 [?
      printi "--More-- 00
      !&end
    ]@
    STH2rk #05 ADD LDA ( type )
    DUP .typ/inner NEQ [?
      LIT2r 0008 ADD2r \\ the actual item is the next line
    ][
      LIT2 -typ/subject -Type LDZ EQU [?
        \\ Not a subject or nesthint block; it contains text (possibly encrypted)
        DUP .typ/break EQU [?
          STH2rk seek
          plaintext \\ TODO
        ][
          printi "-- 00
        ]@
        POP !&next
      ]@
    ]@
    \\ It is an item in a subject or nesthint block
    typech STH2rk seek skipline plaintext
    &next \\ Next item
    LITr 06 ADDr LDA2r
    INC DUP .Height LDZ LTH
  ]?
  &end
  \\ Scroll indicator
  POP2r #0000 locate
  .Height LDZ NEQ DUP ADD
  .Active LDZ2 #0010 ADD2 .Scroll LDZ2 EQU2
  ADD #1d ADD drawch
  BRK

@cls ( -- )
  #0000 #28 DEO2k INC INC DEO2 #802e DEO JMP2r

@seek ( node* -- )
  STH2k LDA2 #a000 STH2rk STA2 STH2rk #02 DEO2 STH2r STA2 JMP2r

@locate ( x y -- )
  #0005 SFT2 #2a DEO2 #0005 SFT2 #28 DEO2 JMP2r

  85 80 81 81 82 83 84 2d 2d 84
@typech ( type -- )
  #0e SUB LDR
  \\ fall through

@drawch ( char -- )
  #0005 SFT2 ;Font ADD2 #2c DEO2 LIT2 &ink 012f DEO JMP2r

@printi ( -- )
  [@ LDArk STHr INC2r DUP [? POP JMP2r ]@ drawch ]!

@rvon ( -- )
  LIT2 04 \,drawch/ink STR JMP2r

@rvoff ( -- )
  LIT2 01 \,drawch/ink STR JMP2r

@showtitle ( -- )
  rvon #0100 locate
  .Type LDZ typech
  .Active LDZ2 seek skipline
@plaintext ( -- )
  [@ #ac DEI DUP #0d NEQ [? POP JMP2r ]@ drawch ]!

@error
  [@ STH2rk INC2r LDA DUP #19 DEO #0a EOR ]?
  #010f DEO BRK

@skipline ( -- )
  [@ #0aac DEI NEQ ]? JMP2r

@isword ( char -- )
  DUP LDArk STHr INC2r EQU [?
    [@ LDArk STHr INC2r #0d NEQ ]? JMP2r
  ]@
  POP LDArk STHr
  [@
    INC2r
    #ac DEI
    DUP #0a NEQ [? POP2r ?makeindex/el ]@
    DUP LDArk STHr EQU [? POP2r ?makeindex/nn ]@
    #0d NEQ [? POP2r #ac DEI ?makeindex/set ]@
  ]!

@decode ( char -- char )
  DUP #21 LTH [?
    DUP ADD DUP #9f GTH #5f MUL SUB
  ]@ JMP2r

@vkey
  hidemouse
  \\ Keyboard
  
  #82 DEI .Jstate STH2k LDZ STZr EQU ?brk
    \\ Joypad
    #0182 DEI EQU ?kEnter
    #0282 DEI EQU ?kExit
    #1082 DEI EQU ?kUp
    #2082 DEI EQU ?kDown
    
  @brk BRK

@vmouse
  hidemouse
  #92 DEI2 DUP2 .MouseX STZ2 #28 DEO2
  #94 DEI2 DUP2 .MouseY STZ2 #2a DEO2
  ;mouseshape #2c DEO2 #c12f DEO
  #96 DEI .Mstate STH2k LDZ STZr EQU ?brk
    \\ If mouse button is pushed
    
  BRK

@hidemouse ( -- )
  .MouseX LDZ2 #28 DEO2
  .MouseY LDZ2 #2a DEO2
  ;BlankFont #2c DEO2
  #412f DEO JMP2r

@kEnter
  .Cursor LDZ2 .ActiveEnd LDZ2 EQU2 ?brk
  LIT2 -typ/subject -Type LDZ EQU [?
    LIT2 -typ/nesthint -Type LDZ NEQ ?brk
    LIT2 -typ/inner -Cursor LDZ2 #05 ADD LDA NEQ ?brk
    #0008
  ][
    #0000
  ]@
  .Cursor LDZ2 ADD2
  DUP2 #05 ADD LDA DUP .typ/blank LTH MUL [? POP2 BRK ]@
  !gonode

@kExit
  .Active LDZ2 LDA2 !gonode

@kUp
  LITr -Cursor LDZ2r
  .Active LDZ2 #0010 ADD2
  [@
    DUP2 STH2rk NEQ2 [? POP2 POP2r !shownode ]@
    DUP2 #05 ADD LDA .typ/inner EQU [? DUP2 .Cursor STZ2 ]@
    #06 ADD LDA2
  ]!

@kDown
  .Cursor LDZ2 INC2 .ActiveEnd LDZ2 GTH2 ?brk
  .Cursor LDZ2 .Break LDZ2 EQU2 ?brk
  .Cursor LDZ2k
    LIT2 -typ/inner -Cursor LDZ2 #05 ADD LDA NEQ [? #0008 ADD2 ]@
  #06 ADD LDA2 ROT STZ2
  !shownode

\_@2+fe'&(@2+!) \\ alignment
@Tkey "yke

\_@8@7&-7&+! ( alignment )

@mouseshape
  \=2222222.
  \=2333332.
  \=2332222.
  \=23232...
  \=232232..
  \=232.232.
  \=222..232
  \=......22;

( 1c )
  \-........
  \-.11.....
  \-.1111...
  \-.111111.
  \-.1111...
  \-.11.....
  \-........
  \-........;

( 1d )
  \-...1....
  \-..1.1...
  \-.1...1..
  \-1.....1.
  \-1.....1.
  \-.1...1..
  \-..1.1...
  \-...1....;

( 1e )
  \-........
  \-........
  \-........
  \-........
  \-1.....1.
  \-.1...1..
  \-..1.1...
  \-...1....;

( 1f )
  \-...1....
  \-..1.1...
  \-.1...1..
  \-1.....1.
  \-........
  \-........
  \-........
  \-........;

\_@20'8*- \@Font @BlankFont \~ascii.font

( 80 - subject )
  \-........
  \-........
  \-..1111..
  \-..1111..
  \-..1111..
  \-..1111..
  \-........
  \-........;

( 81 - hint,nesthint )
  \-........
  \-...1....
  \-..111...
  \-.11111..
  \-1111111.
  \-.11111..
  \-..111...
  \-...1....;

( 82 - comment )
  \-........
  \-..1111..
  \-.1....1.
  \-.1....1.
  \-.1....1.
  \-.1....1.
  \-..1111..
  \-........;

( 83 - credit )
  \-........
  \-..1111..
  \-.1.1..1.
  \-.1..1.1.
  \-.1.1..1.
  \-.1..1.1.
  \-..1111..
  \-........;

( 84 - link )
  \-........
  \-.1.1....
  \-..1.1...
  \-...1.1..
  \-.1111.1.
  \-...1.1..
  \-..1.1...
  \-.1.1....;

( 85 - unknown type )
  \-........
  \-1111111.
  \-11...11.
  \-1.111.1.
  \-1111.11.
  \-111.111.
  \-1111111.
  \-111.111.;

\\ Extra records (explained below)
@line-1 :line-1 00 00 00 -typ/subject &final ffff
@line0 0000 00 00 00 -typ/inner :lines

@lines
  \\ List of lines of UHS file is stored in here.
  \\ (This area is also used for some things only used during initialization)
  \\ The format is:
  \\   Address of record for parent node (two bytes)
  \\   File offset (three bytes)
  \\   Item type (one byte)
  \\   Address of record for line following end of block (two bytes)
  \_@8+ \@line2
  \_@b+ \@line2z
  \_@8+b+ \@line3z

@endof88a
  0a "** 20 "END 20 "OF 20 "88A 20 "FORMAT 20 "** 0d0a &end

@notuhs
  error "Not 20 "a 20 "UHS 20 "file 0a

@started
  #17 DEI [?
    error "usage: 20 "uhsview.rom 20 "<uhsfile> 0a
  ]@
  \\ Read file name
  LIT2r :filename DUP2r
  [; #10 DEO2 BRK ]@
  #17 DEI #02 GTH [? #12 DEI STH2rk INC2r STA BRK ]@
  \\ Finish with file name
  #00 DUPk STH2r STA #10 DEO2
  LITr a8 DEO2r
  \\ Check file existence and if it is UHS
  #ac DEI2
  #a3 DEI [? error "Cannot 20 "read 20 "file 0a ]@
  LIT2 "UH NEQ2 ?notuhs
  #ac DEI2 LIT2 "S 0d NEQ2 ?notuhs
  #ac DEI #0a NEQ ?notuhs
  \\ Find the "** END OF 88A FORMAT **"
  LIT2r :endof88a DUP2r
  [@
    #a3 DEI [?
      error "This 20 "program 20 "is 20 "not 20 "compatible 20 "with 20 "88a 20 "format 0a
    ]@
    #ac DEI STH2rk INC2r LDA EQU [? POP2r DUP2r ]@
    STH2rk ;endof88a/end NEQ2
  ]?
  POP2r POP2r
  \\ Device initialization
  #d0f0 #08 DEO2
  #d0f2 #0a DEO2
  #c0f1 #0c DEO2
  #0278 #22 DEO2
  #00c8 #24 DEO2
  #0126 DEO
  ;vkey #80 DEO2
  ;vmouse #90 DEO2
  #2224 [& ]@ DEI2k #03 SFT2 SWP #00 NEQ #ff MUL ORA SWP STZ JMP2r

@filename
  \\ File name storage

@typ |00
  &none $1
  &subject $1
  &hint $1
  &nesthint $1
  &comment $1
  &credit $1
  &link $1
  &blank $1
  &break $1
  &inner $1
  \:typ/subject \:typ/nesthint \_^ \@subxnest

